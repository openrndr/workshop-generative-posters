import org.gradle.internal.os.OperatingSystem

group 'org.openrndr.workshop'
version '1.0'

buildscript {
    ext.kotlin_version = '1.3.0'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

ext.orx_version = "0.0.19"
ext.rndr_version = '0.3.32-rc2'
ext.panel_version = '0.3.3'
ext.slf4j_version = '1.7.25'
ext.rndr_gl3_natives = ""

switch (OperatingSystem.current()) {
    case OperatingSystem.WINDOWS:
        ext.rndr_gl3_natives = "openrndr-gl3-natives-windows"
        break
    case OperatingSystem.MAC_OS:
        ext.rndr_gl3_natives = "openrndr-gl3-natives-macos"
        break
    case OperatingSystem.LINUX:
        ext.rndr_gl3_natives = "openrndr-gl3-natives-linux-x64"
        break

}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = 1.8
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = "https://dl.bintray.com/openrndr/openrndr/"
        }
        maven { url "https://jitpack.io" }
    }
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile "org.openrndr:openrndr-core:$rndr_version"
        compile "org.openrndr:openrndr-animatable:$rndr_version"
        compile "org.openrndr:openrndr-svg:$rndr_version"
        compile "org.openrndr:openrndr-ffmpeg:$rndr_version"
        compile "org.openrndr:openrndr-extensions:$rndr_version"
        compile "org.openrndr:openrndr-filter:$rndr_version"
        compile "org.openrndr.panel:openrndr-panel:$panel_version"
        runtime "org.openrndr:openrndr-gl3:$rndr_version"
        runtime "org.openrndr:$rndr_gl3_natives:$rndr_version"
        implementation "com.github.openrndr.orx:orx-compositor:v$orx_version"
    }
}

project(":toolkit"){
    apply plugin: 'maven-publish'
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = 'org.openrndr.workshop'
                artifactId = 'toolkit'
                version = '0.0.1-SNAPSHOT'
                from components.java
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

